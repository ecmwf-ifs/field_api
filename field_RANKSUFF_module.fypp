#! (C) Copyright 2022- ECMWF.
#! (C) Copyright 2022- Meteo-France.
#!
#! This software is licensed under the terms of the Apache Licence Version 2.0
#! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
#! In applying this licence, ECMWF does not waive the privileges and immunities
#! granted to it by virtue of its status as an intergovernmental organisation
#! nor does it submit to any jurisdiction.

MODULE FIELD_${RANK}$${SUFF}$_MODULE

#:set fieldTypeList = fieldType.getFieldTypeList (ranks=[RANK], kinds=['JP' + str (SUFF)])

USE OML_MOD, ONLY: OML_MAX_THREADS , OML_MY_THREAD 
USE IEEE_ARITHMETIC, ONLY: IEEE_SIGNALING_NAN
USE DEV_ALLOC_MODULE
USE FIELD_BASIC_MODULE
USE FIELD_INIT_DEBUG_VALUE_MODULE
${fieldType.useParkind1 ()}$

IMPLICIT NONE

PRIVATE

INTEGER (KIND=JPIM), PARAMETER :: NDEVFRESH =      INT(B'00000001', KIND=JPIM)
INTEGER (KIND=JPIM), PARAMETER :: NHSTFRESH =      INT(B'00000010', KIND=JPIM)
INTEGER (KIND=JPIM), PARAMETER :: UNALLOCATED =    INT(B'00000100', KIND=JPIM)
INTEGER (KIND=JPIM), PARAMETER :: UNINITIALIZED =  INT(B'00000011', KIND=JPIM)
INTEGER (KIND=JPIM), PARAMETER :: NH2D = 1, ND2H = 2
INTEGER (KIND=JPIM), PARAMETER :: NRD = INT(B'00000001', KIND=JPIM)
INTEGER (KIND=JPIM), PARAMETER :: NWR = INT(B'00000010', KIND=JPIM)


#:for ft in fieldTypeList
#:set ftn = ft.name
TYPE, ABSTRACT, EXTENDS (FIELD_BASIC) :: ${ftn}$
  ${ft.type}$, POINTER :: PTR(${ft.shape}$) => NULL()
  ${ft.type}$, POINTER, CONTIGUOUS :: DEVPTR(${ft.shape}$) => NULL()
CONTAINS

  PROCEDURE :: FINAL => ${ftn}$_FINAL
  PROCEDURE :: ${ftn}$_FINAL
  PROCEDURE :: DELETE_DEVICE => ${ftn}$_DELETE_DEVICE
  PROCEDURE :: GET_VIEW => ${ftn}$_GET_VIEW
  PROCEDURE :: GET_DEVICE_DATA_RDONLY => ${ftn}$_GET_DEVICE_DATA_RDONLY
  PROCEDURE :: GET_DEVICE_DATA_RDWR => ${ftn}$_GET_DEVICE_DATA_RDWR
  PROCEDURE :: GET_HOST_DATA_RDONLY => ${ftn}$_GET_HOST_DATA_RDONLY
  PROCEDURE :: GET_HOST_DATA_RDWR => ${ftn}$_GET_HOST_DATA_RDWR
  PROCEDURE :: SYNC_HOST_RDWR => ${ftn}$_SYNC_HOST_RDWR
  PROCEDURE :: SYNC_HOST_RDONLY => ${ftn}$_SYNC_HOST_RDONLY
  PROCEDURE :: SYNC_DEVICE_RDWR => ${ftn}$_SYNC_DEVICE_RDWR
  PROCEDURE :: SYNC_DEVICE_RDONLY => ${ftn}$_SYNC_DEVICE_RDONLY
  PROCEDURE :: COPY_OBJECT => ${ftn}$_COPY_OBJECT
  PROCEDURE :: WIPE_OBJECT => ${ftn}$_WIPE_OBJECT

  PROCEDURE, PRIVATE :: GET_DEVICE_DATA => ${ftn}$_GET_DEVICE_DATA
  PROCEDURE, PRIVATE :: GET_HOST_DATA => ${ftn}$_GET_HOST_DATA
  PROCEDURE, PRIVATE :: ${ftn}$_GET_HOST_DATA
  PROCEDURE, PRIVATE :: ${ftn}$_GET_DEVICE_DATA
  PROCEDURE, PRIVATE :: COPY_DATA =>  ${ftn}$_COPY_DATA
  PROCEDURE, PRIVATE :: CREATE_DEVICE_DATA => ${ftn}$_CREATE_DEVICE_DATA
END TYPE ${ftn}$

PUBLIC :: ${ftn}$

TYPE, EXTENDS(${ftn}$) :: ${ftn}$_WRAPPER
CONTAINS
  PROCEDURE :: INIT => ${ftn}$_WRAP
  PROCEDURE :: FINAL => ${ftn}$_WRAPPER_FINAL
END TYPE ${ftn}$_WRAPPER

PUBLIC :: ${ftn}$_WRAPPER

TYPE, EXTENDS(${ftn}$) :: ${ftn}$_OWNER
  INTEGER(KIND=JPIM) :: LBOUNDS(${ft.rank}$), UBOUNDS(${ft.rank}$)
  LOGICAL :: HAS_INIT_VALUE = .FALSE.
  ${ft.type}$ :: INIT_VALUE
CONTAINS
  PROCEDURE :: INIT => ${ftn}$_OWNER_INIT
  PROCEDURE :: FINAL => ${ftn}$_OWNER_FINAL
  PROCEDURE, PRIVATE :: ALLOCATE => ${ftn}$_ALLOCATE
  PROCEDURE, PRIVATE :: GET_HOST_DATA => ${ftn}$_OWNER_GET_HOST_DATA
  PROCEDURE, PRIVATE :: GET_DEVICE_DATA => ${ftn}$_OWNER_GET_DEVICE_DATA
END TYPE ${ftn}$_OWNER

PUBLIC :: ${ftn}$_OWNER

TYPE ${ftn}$_PTR
  CLASS(${ftn}$), POINTER :: PTR => NULL()
END TYPE ${ftn}$_PTR

PUBLIC :: ${ftn}$_PTR

TYPE ${ftn}$_VIEW
  ${ft.type}$, POINTER :: P(${ft.viewShape}$) => NULL()
END TYPE ${ftn}$_VIEW

PUBLIC :: ${ftn}$_VIEW

#:endfor

CONTAINS

#:for ft in fieldTypeList
#:set ftn = ft.name
  SUBROUTINE ${ftn}$_WRAP(SELF, DATA, PERSISTENT, LBOUNDS)
    ! Create FIELD object by wrapping existing data
    CLASS(${ftn}$_WRAPPER), INTENT(INOUT) :: SELF
    ${ft.type}$, TARGET, INTENT(IN) :: DATA(${ft.shape}$)
    LOGICAL, INTENT(IN), OPTIONAL :: PERSISTENT
    INTEGER(KIND=JPIM), INTENT(IN), OPTIONAL :: LBOUNDS(${ft.rank}$)
    LOGICAL :: LLPERSISTENT

    LLPERSISTENT = .TRUE.
    IF (PRESENT (PERSISTENT)) LLPERSISTENT = PERSISTENT

    IF (PRESENT(LBOUNDS)) THEN
      SELF%PTR(${ft.lbptr}$) => DATA
    ELSE
      SELF%PTR => DATA
    ENDIF
    SELF%THREAD_BUFFER = .NOT. LLPERSISTENT
    SELF%ISTATUS = NHSTFRESH

    IF (.NOT. LLPERSISTENT) THEN
      IF (OML_MAX_THREADS () /= SIZE (DATA, ${ft.rank}$)) THEN
        CALL ABOR1 ('${ftn}$_WRAP: DIMENSION MISMATCH')
      ENDIF
    ENDIF

  END SUBROUTINE ${ftn}$_WRAP

  SUBROUTINE ${ftn}$_OWNER_INIT (SELF, LBOUNDS, UBOUNDS, PERSISTENT, DELAYED, INIT_VALUE)
    CLASS(${ftn}$_OWNER) :: SELF
    INTEGER(KIND=JPIM), INTENT(IN), OPTIONAL :: LBOUNDS(${ft.rank}$)
    INTEGER(KIND=JPIM), INTENT(IN) :: UBOUNDS(${ft.rank}$)
    LOGICAL, OPTIONAL,  INTENT(IN) :: PERSISTENT
    LOGICAL, OPTIONAL,  INTENT(IN) :: DELAYED
    ${ft.type}$, OPTIONAL, INTENT(IN) :: INIT_VALUE
    LOGICAL :: IS_DELAYED

    IS_DELAYED = .FALSE.
    IF(PRESENT(DELAYED))THEN
      IS_DELAYED = DELAYED
    ENDIF

    IF (PRESENT (LBOUNDS)) THEN
      SELF%LBOUNDS=LBOUNDS
    ELSE
      SELF%LBOUNDS=1
    ENDIF
    SELF%UBOUNDS=UBOUNDS
    SELF%UBOUNDS(${ft.rank}$) = OML_MAX_THREADS ()

    ! By default we allocate thread-local temporaries
    SELF%THREAD_BUFFER = .TRUE.

    SELF%LAST_CONTIGUOUS_DIMENSION = ${ft.rank}$

    IF (PRESENT(PERSISTENT)) THEN
      IF (PERSISTENT) THEN
        SELF%THREAD_BUFFER = .FALSE.
        SELF%LBOUNDS(${ft.rank}$) = 1
        SELF%UBOUNDS(${ft.rank}$) = UBOUNDS(${ft.rank}$)
      END IF
    END IF

    SELF%ISTATUS = UNALLOCATED
    IF (PRESENT(INIT_VALUE)) THEN
      SELF%HAS_INIT_VALUE=.TRUE.
      SELF%INIT_VALUE=INIT_VALUE
    ELSE IF(USE_INIT_DEBUG_VALUE) THEN
      SELF%HAS_INIT_VALUE=.TRUE.
      SELF%INIT_VALUE=INIT_DEBUG_VALUE_${ft.kind}$
    ENDIF
    IF(.NOT. IS_DELAYED) THEN
      CALL SELF%ALLOCATE()
      IF (SELF%HAS_INIT_VALUE) THEN
        SELF%PTR=SELF%INIT_VALUE
	SELF%ISTATUS=NHSTFRESH
      ENDIF
    ENDIF
  END SUBROUTINE ${ftn}$_OWNER_INIT

  SUBROUTINE ${ftn}$_ALLOCATE (SELF)
    ! Create FIELD object by explicitly allocating new data
    CLASS(${ftn}$_OWNER) :: SELF

    ALLOCATE(SELF%PTR(${', '.join(map(lambda r: 'SELF%LBOUNDS('+str(r+1)+'):SELF%UBOUNDS('+str(r+1)+')', range(0, ft.rank)))}$))
    SELF%ISTATUS = UNINITIALIZED
  END SUBROUTINE ${ftn}$_ALLOCATE

  FUNCTION ${ftn}$_GET_VIEW(SELF, BLOCK_INDEX, ZERO) RESULT(VIEW_PTR)
    CLASS(${ftn}$) :: SELF
    ${ft.type}$, POINTER :: VIEW_PTR(${ft.viewShape}$)
    INTEGER(KIND=JPIM), INTENT(IN) :: BLOCK_INDEX
    LOGICAL, OPTIONAL,  INTENT(IN) :: ZERO
    INTEGER(KIND=JPIM) :: IDX
    INTEGER(KIND=JPIM) :: LBOUNDS(${ft.rank}$)
#include "abor1.intfb.h"

    IF(IAND(SELF%ISTATUS,NHSTFRESH)/=NHSTFRESH) THEN
      CALL ABOR1 ("GET_VIEW WAS CALLED, BUT DATA IS NOT PRESENT ON HOST")
    ENDIF

    IDX = BLOCK_INDEX
    IF (SELF%THREAD_BUFFER) IDX = OML_MY_THREAD ()

    LBOUNDS=LBOUND(SELF%PTR)
    VIEW_PTR(${','.join('LBOUNDS(%d):'%(r+1) for r in range(ft.viewRank))}$) => SELF%PTR(${','.join(':' for _ in range(ft.viewRank))}$,IDX)

    IF (PRESENT(ZERO)) THEN
      IF (ZERO) VIEW_PTR(${ft.viewShape}$) = ${ft.default}$
    END IF

! Mark data dirty on the device: the pointer returned by GET_VIEW may be rw
    IF (IAND (SELF%ISTATUS, NDEVFRESH)==NDEVFRESH) THEN
      SELF%ISTATUS = IAND(SELF%ISTATUS, NOT(NDEVFRESH))
    ENDIF
  END FUNCTION ${ftn}$_GET_VIEW

  SUBROUTINE ${ftn}$_DELETE_DEVICE(SELF)
    ! Delete the copy of this field on GPU device
    CLASS(${ftn}$) :: SELF

    IF (ASSOCIATED (SELF%DEVPTR)) THEN
      CALL DEV_DEALLOCATE (SELF%DEVPTR)
    ENDIF
  END SUBROUTINE ${ftn}$_DELETE_DEVICE

  SUBROUTINE ${ftn}$_FINAL(SELF)
    ! Finalizes field and deallocates owned data
    CLASS(${ftn}$) :: SELF
    NULLIFY(SELF%PTR)
    CALL SELF%DELETE_DEVICE()
  END SUBROUTINE ${ftn}$_FINAL

  SUBROUTINE ${ftn}$_WRAPPER_FINAL(SELF)
    ! Finalizes field and deallocates owned data
    CLASS(${ftn}$_WRAPPER) :: SELF
    ${ft.type}$, POINTER :: PTR(${ft.shape}$)
    CALL SELF%GET_HOST_DATA_RDONLY(PTR)
    CALL SELF%${ftn}$_FINAL
  END SUBROUTINE ${ftn}$_WRAPPER_FINAL

  SUBROUTINE ${ftn}$_OWNER_FINAL(SELF)
    ! Finalizes field and deallocates owned data
    CLASS(${ftn}$_OWNER) :: SELF
    IF (ASSOCIATED(SELF%PTR)) THEN
      DEALLOCATE(SELF%PTR)
    END IF
    CALL SELF%${ftn}$_FINAL
  END SUBROUTINE ${ftn}$_OWNER_FINAL

  SUBROUTINE ${ftn}$_COPY_OBJECT (SELF, LDCREATED)
  CLASS(${ftn}$) :: SELF
  LOGICAL, INTENT (IN), OPTIONAL :: LDCREATED
  LOGICAL :: LLCREATED

  LLCREATED = .FALSE.
  IF (PRESENT (LDCREATED)) LLCREATED = LDCREATED

  IF (.NOT. LLCREATED) STOP 1

  IF (.NOT. SELF%LOBJECT_COPIED) THEN
    SELF%LOBJECT_COPIED = .TRUE.
#ifdef _OPENACC
    IF (ASSOCIATED (SELF%DEVPTR)) THEN
      !$acc enter data attach (SELF%DEVPTR)
    ENDIF
#endif
  ENDIF

  END SUBROUTINE ${ftn}$_COPY_OBJECT

  SUBROUTINE ${ftn}$_WIPE_OBJECT (SELF, LDDELETED)
  CLASS(${ftn}$) :: SELF
  LOGICAL, INTENT (IN), OPTIONAL :: LDDELETED
  LOGICAL :: LLDELETED

  LLDELETED = .FALSE.
  IF (PRESENT (LDDELETED)) LLDELETED = LDDELETED

  IF (.NOT. LLDELETED) STOP 1

  IF (SELF%LOBJECT_COPIED) THEN
    SELF%LOBJECT_COPIED = .FALSE.
#ifdef _OPENACC
    IF (ASSOCIATED (SELF%DEVPTR)) THEN
      !$acc exit data detach (SELF%DEVPTR)
    ENDIF
#endif
  ENDIF

  END SUBROUTINE ${ftn}$_WIPE_OBJECT

  SUBROUTINE ${ftn}$_COPY_DATA (SELF, KDIR, QUEUE)
  CLASS(${ftn}$) :: SELF
  INTEGER (KIND=JPIM),           INTENT(IN) :: KDIR
  INTEGER (KIND=JPIM), OPTIONAL, INTENT(IN) :: QUEUE
  REAL :: START, FINISH

  SELF%LAST_CONTIGUOUS_DIMENSION = ${ftn}$_GET_LAST_CONTIGUOUS_DIMENSION (SELF%PTR)

  CALL CPU_TIME(START)
  SELECT CASE (SELF%LAST_CONTIGUOUS_DIMENSION)
#:for d in range (ft.rank + 1)
    CASE (${d}$)
      CALL COPY_DIM${d}$_CONTIGUOUS (SELF%PTR, SELF%DEVPTR, KDIR, QUEUE)
#:endfor
  END SELECT
  CALL CPU_TIME(FINISH)
  IF (KDIR == NH2D) THEN
    CALL SELF%STATS%INC_CPU_TO_GPU_TRANSFER(START, FINISH)
  ELSE IF (KDIR == ND2H) THEN
    CALL SELF%STATS%INC_GPU_TO_CPU_TRANSFER(START, FINISH)
  END IF

  CONTAINS

#:for d in range (0, ft.rank+1)
    SUBROUTINE COPY_DIM${d}$_CONTIGUOUS (HST, DEV, KDIR, QUEUE)
#ifdef _OPENACC
    USE OPENACC
#endif
    ${ft.type}$, POINTER :: HST (${ft.shape}$), DEV (${ft.shape}$)
    INTEGER (KIND=JPIM),           INTENT (IN) :: KDIR
    INTEGER (KIND=JPIM), OPTIONAL, INTENT (IN) :: QUEUE
    INTEGER (KIND=JPIM) :: ISIZE
    INTEGER :: ${', '.join (['J'] + list (map (lambda i: 'J' + str (i+1), range (d, ft.rank))))}$

  #:for e in range (ft.rank, d, -1)
    ${'  ' * (ft.rank - e)}$DO J${e}$ = LBOUND (HST, ${e}$), UBOUND (HST, ${e}$)
  #:endfor
  #:set ar = ', '.join ([':'] * d  + list (map (lambda i: 'J' + str (i+1), range (d, ft.rank))))
  #:set indent = '  ' * (ft.rank - e)
    #:if d == 0
    ${indent}$  ISIZE = KIND (HST)
    #:else
    ${indent}$  ISIZE = KIND (HST) * SIZE (HST (${ar}$))
    #:endif
    ${indent}$  IF (KDIR == NH2D) THEN
#ifdef _OPENACC
    ${indent}$    !$acc host_data use_device (DEV)
    ${indent}$    IF(PRESENT(QUEUE))THEN
    ${indent}$      CALL ACC_MEMCPY_TO_DEVICE_ASYNC (DEV (${ar}$), HST (${ar}$), ISIZE, QUEUE)
    ${indent}$    ELSE
    ${indent}$      CALL ACC_MEMCPY_TO_DEVICE (DEV (${ar}$), HST (${ar}$), ISIZE)
    ${indent}$    ENDIF
    ${indent}$    !$acc end host_data
#else
    ${indent}$    DEV (${ar}$) = HST (${ar}$)
#endif
    ${indent}$  ELSEIF (KDIR == ND2H) THEN
#ifdef _OPENACC
    ${indent}$    !$acc host_data use_device (DEV)
    ${indent}$    IF(PRESENT(QUEUE))THEN
    ${indent}$      CALL ACC_MEMCPY_FROM_DEVICE_ASYNC (HST (${ar}$), DEV (${ar}$), ISIZE, QUEUE)
    ${indent}$    ELSE
    ${indent}$      CALL ACC_MEMCPY_FROM_DEVICE (HST (${ar}$), DEV (${ar}$), ISIZE)
    ${indent}$    ENDIF
    ${indent}$    !$acc end host_data
#else
    ${indent}$    HST (${ar}$) = DEV (${ar}$)
#endif
    ${indent}$  ENDIF
  #:for e in range (d, ft.rank)
    ${'  ' * (ft.rank - e - 1)}$ENDDO
  #:endfor
    END SUBROUTINE

#:endfor

  END SUBROUTINE ${ftn}$_COPY_DATA


  SUBROUTINE ${ftn}$_GET_HOST_DATA (SELF, MODE, PTR, QUEUE)
    CLASS(${ftn}$),                INTENT(INOUT) :: SELF
    INTEGER (KIND=JPIM),           INTENT(IN)    :: MODE

    ${ft.type}$, POINTER,          INTENT(INOUT) :: PTR(${ft.shape}$)
    INTEGER (KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE

    INTEGER(KIND=JPIM) :: LBOUNDS(${ft.rank}$)

    LBOUNDS=LBOUND(SELF%PTR)
    IF (IAND (SELF%ISTATUS, NHSTFRESH) == 0) THEN
      CALL SELF%COPY_DATA (ND2H, QUEUE)
      SELF%ISTATUS = IOR (SELF%ISTATUS, NHSTFRESH)
    ENDIF
    PTR (${ft.lbptr}$) => SELF%PTR (${','.join(':' for _ in range(ft.rank))}$)
    IF (IAND (MODE, NWR) /= 0) THEN
      SELF%ISTATUS = IAND (SELF%ISTATUS, NOT (NDEVFRESH))
    ENDIF

  END SUBROUTINE ${ftn}$_GET_HOST_DATA

  SUBROUTINE ${ftn}$_OWNER_GET_HOST_DATA (SELF, MODE, PTR, QUEUE)
    CLASS(${ftn}$_OWNER),          INTENT(INOUT) :: SELF
    INTEGER (KIND=JPIM),           INTENT(IN)    :: MODE
    ${ft.type}$, POINTER,          INTENT(INOUT) :: PTR(${ft.shape}$)
    INTEGER (KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE

    IF(SELF%ISTATUS==UNALLOCATED)THEN
      CALL SELF%ALLOCATE()
      IF (SELF%HAS_INIT_VALUE) THEN
        SELF%PTR=SELF%INIT_VALUE
	SELF%ISTATUS=NHSTFRESH
      ENDIF
    ENDIF
    CALL SELF%${ftn}$_GET_HOST_DATA(MODE, PTR, QUEUE)

  END SUBROUTINE ${ftn}$_OWNER_GET_HOST_DATA

  SUBROUTINE ${ftn}$_GET_HOST_DATA_RDONLY (SELF, PPTR, QUEUE)
    CLASS(${ftn}$),               INTENT(INOUT) :: SELF
    ${ft.type}$, POINTER,         INTENT(INOUT) :: PPTR(${ft.shape}$)
    INTEGER(KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE

    CALL SELF%GET_HOST_DATA (NRD, PPTR, QUEUE)

  END SUBROUTINE ${ftn}$_GET_HOST_DATA_RDONLY

  SUBROUTINE ${ftn}$_SYNC_HOST_RDONLY (SELF, QUEUE)
    CLASS(${ftn}$),               INTENT(INOUT) :: SELF
    INTEGER(KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE
    ${ft.type}$, POINTER                        :: ZPTR(${ft.shape}$)

    CALL SELF%GET_HOST_DATA_RDONLY (ZPTR, QUEUE)

  END SUBROUTINE ${ftn}$_SYNC_HOST_RDONLY

  SUBROUTINE ${ftn}$_GET_HOST_DATA_RDWR (SELF, PPTR, QUEUE)
    CLASS(${ftn}$),   INTENT(INOUT) :: SELF
    ${ft.type}$, POINTER,         INTENT(INOUT) :: PPTR(${ft.shape}$)
    INTEGER(KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE

    CALL SELF%GET_HOST_DATA (IOR (NRD, NWR), PPTR, QUEUE)

  END SUBROUTINE ${ftn}$_GET_HOST_DATA_RDWR

  SUBROUTINE ${ftn}$_SYNC_HOST_RDWR (SELF, QUEUE)
    CLASS(${ftn}$),               INTENT(INOUT) :: SELF
    INTEGER(KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE
    ${ft.type}$, POINTER                        :: ZPTR(${ft.shape}$)

    CALL SELF%GET_HOST_DATA_RDWR (ZPTR, QUEUE)

  END SUBROUTINE ${ftn}$_SYNC_HOST_RDWR

  SUBROUTINE ${ftn}$_CREATE_DEVICE_DATA (SELF)
    CLASS(${ftn}$), INTENT (INOUT) :: SELF
    CALL DEV_ALLOCATE_HST (DEV=SELF%DEVPTR, HST=SELF%PTR)
  END SUBROUTINE

  SUBROUTINE ${ftn}$_GET_DEVICE_DATA (SELF, MODE, PTR, QUEUE)
    CLASS(${ftn}$),                INTENT(INOUT) :: SELF
    INTEGER (KIND=JPIM),           INTENT(IN)    :: MODE
    ${ft.type}$, POINTER,          INTENT(INOUT) :: PTR(${ft.shape}$)
    INTEGER (KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE
    INTEGER(KIND=JPIM)                           :: LBOUNDS(${ft.rank}$)

    LBOUNDS=LBOUND(SELF%PTR)
    IF (.NOT. ASSOCIATED (SELF%DEVPTR)) THEN
      CALL SELF%CREATE_DEVICE_DATA
    ENDIF
    IF (IAND (SELF%ISTATUS, NDEVFRESH) == 0) THEN
      CALL SELF%COPY_DATA (NH2D, QUEUE)
      SELF%ISTATUS = IOR (SELF%ISTATUS, NDEVFRESH)
    ENDIF
    PTR (${ft.lbptr}$) => SELF%DEVPTR (${','.join(':' for _ in range(ft.rank))}$)
    IF (IAND (MODE, NWR) /= 0) THEN
      SELF%ISTATUS = IAND (SELF%ISTATUS, NOT (NHSTFRESH))
    ENDIF

  END SUBROUTINE ${ftn}$_GET_DEVICE_DATA

  SUBROUTINE ${ftn}$_OWNER_GET_DEVICE_DATA (SELF, MODE, PTR, QUEUE)
    CLASS(${ftn}$_OWNER),          INTENT(INOUT) :: SELF
    INTEGER (KIND=JPIM),           INTENT(IN)    :: MODE
    ${ft.type}$, POINTER,          INTENT(INOUT) :: PTR(${ft.shape}$)
    INTEGER (KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE

    IF(SELF%ISTATUS==UNALLOCATED)THEN
      CALL SELF%ALLOCATE()
      IF (SELF%HAS_INIT_VALUE) THEN
        CALL SELF%CREATE_DEVICE_DATA
        SELF%PTR=SELF%INIT_VALUE
	SELF%ISTATUS=NHSTFRESH
      ENDIF
    ENDIF
    CALL SELF%${ftn}$_GET_DEVICE_DATA(MODE, PTR, QUEUE)

  END SUBROUTINE ${ftn}$_OWNER_GET_DEVICE_DATA

  SUBROUTINE ${ftn}$_GET_DEVICE_DATA_RDONLY (SELF, PPTR, QUEUE)
    CLASS(${ftn}$),               INTENT(INOUT) :: SELF
    ${ft.type}$, POINTER,         INTENT(INOUT) :: PPTR(${ft.shape}$)
    INTEGER(KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE

    CALL SELF%GET_DEVICE_DATA (NRD, PPTR, QUEUE)

  END SUBROUTINE ${ftn}$_GET_DEVICE_DATA_RDONLY

  SUBROUTINE ${ftn}$_SYNC_DEVICE_RDONLY (SELF, QUEUE)
    CLASS(${ftn}$),               INTENT(INOUT) :: SELF
    INTEGER(KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE
    ${ft.type}$, POINTER                        :: ZPTR(${ft.shape}$)

    CALL SELF%GET_DEVICE_DATA_RDONLY (ZPTR, QUEUE)

  END SUBROUTINE ${ftn}$_SYNC_DEVICE_RDONLY

  SUBROUTINE ${ftn}$_GET_DEVICE_DATA_RDWR (SELF, PPTR, QUEUE)
    CLASS(${ftn}$),               INTENT(INOUT) :: SELF
    ${ft.type}$, POINTER,         INTENT(INOUT) :: PPTR(${ft.shape}$)
    INTEGER(KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE

    CALL SELF%GET_DEVICE_DATA (IOR (NRD, NWR), PPTR, QUEUE)

  END SUBROUTINE ${ftn}$_GET_DEVICE_DATA_RDWR

  SUBROUTINE ${ftn}$_SYNC_DEVICE_RDWR (SELF, QUEUE)
    CLASS(${ftn}$),               INTENT(INOUT) :: SELF
    INTEGER(KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE
    ${ft.type}$, POINTER                        :: ZPTR(${ft.shape}$)

    CALL SELF%GET_DEVICE_DATA_RDWR (ZPTR, QUEUE)

  END SUBROUTINE ${ftn}$_SYNC_DEVICE_RDWR

#:endfor

#:for ft in fieldTypeList
#:set ftn = ft.name
  INTEGER (KIND=JPIM) FUNCTION ${ftn}$_GET_LAST_CONTIGUOUS_DIMENSION (PTR) RESULT (JDIM)
  ${ft.type}$, POINTER :: PTR (${ft.shape}$)
  INTEGER*8 :: ISTRIDE (${ft.rank}$)
  INTEGER (KIND=JPIM) :: J, LB(${ft.rank}$)

  LB = LBOUND(PTR)
  ISTRIDE (1) = KIND (PTR)
  DO J = 2, ${ft.rank}$
    ISTRIDE (J) = ISTRIDE (J-1) * SIZE (PTR, J-1)
  ENDDO

  JDIM = 0
#:for d in range (1, ft.rank+1)
  #:set ind0 = ""
  #:set ind1 = ""
  #:for i in range(1,ft.rank+1)
    #:set ind0 = ind0 + "LB({}), ".format(i)
    #:set ind1 = ind1 + "LB({}){}, ".format(i,"+1"*(i==d))
  #:endfor
  #:set ind0 = ind0[:-2]
  #:set ind1 = ind1[:-2]
  IF (LOC (PTR (${ind1}$)) - LOC (PTR (${ind0}$)) /= ISTRIDE (${d}$)) THEN
    RETURN
  ENDIF

  JDIM = ${d}$

#:endfor
  END FUNCTION ${ftn}$_GET_LAST_CONTIGUOUS_DIMENSION
#:endfor

END MODULE FIELD_${RANK}$${SUFF}$_MODULE
