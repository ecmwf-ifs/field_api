! Rank and shape definitions for simple templating
!
! Note that the ranks encode coneptual dimensions here, eg. FIELD_2D encodes
! a surface field and FIELD_3D represents a field with a vertical component.
#:include "field_definition.fypp"

MODULE FIELD_MODULE
  ! The FIELD types provided by this module provide data abstractions that
  ! decouple data storage in memory from the data views used in thread-parallel
  ! sections of the code. They are intended to thinly wrap ATLAS_FIELD
  ! objects and provide additional features that may later be
  ! incorporated into Atlas. They can also provide backward-compatibility
  ! for non-Atlas execution modes.

USE PARKIND1, ONLY: JPIM, JPRB
USE OMP_LIB, ONLY: OMP_GET_MAX_THREADS, OMP_GET_THREAD_NUM
USE IEEE_ARITHMETIC, ONLY: IEEE_SIGNALING_NAN

IMPLICIT NONE

INTEGER (KIND=JPIM), PARAMETER :: NDEVFRESH = INT(B'00000001', KIND=JPIM)
INTEGER (KIND=JPIM), PARAMETER :: NHSTFRESH = INT(B'00000010', KIND=JPIM)
INTEGER (KIND=JPIM), PARAMETER :: UNALLOCATED = INT(B'00000100', KIND=JPIM)
INTEGER (KIND=JPIM), PARAMETER, PRIVATE :: NH2D = 1, ND2H = 2
INTEGER (KIND=JPIM), PARAMETER, PRIVATE :: NRD = INT(B'00000001', KIND=JPIM)
INTEGER (KIND=JPIM), PARAMETER, PRIVATE :: NWR = INT(B'00000010', KIND=JPIM)

#ifdef _OPENACC
LOGICAL, PARAMETER :: IS_DEVICE_SIMULATED = .FALSE.
#else
LOGICAL, PARAMETER :: IS_DEVICE_SIMULATED = .TRUE.
#endif

TYPE GPU_STATS
  INTEGER :: TRANSFER_CPU_TO_GPU = 0
  INTEGER :: TRANSFER_GPU_TO_CPU = 0
  REAL :: TOTAL_TIME_TRANSFER_CPU_TO_GPU = 0
  REAL :: TOTAL_TIME_TRANSFER_GPU_TO_CPU = 0
  CONTAINS
  PROCEDURE :: INC_CPU_TO_GPU_TRANSFER
  PROCEDURE :: INC_GPU_TO_CPU_TRANSFER
END TYPE GPU_STATS

TYPE, ABSTRACT :: FIELD_BASIC
  ! Flag indicating the use a single block-buffer per thread
  LOGICAL :: THREAD_BUFFER = .FALSE.

  INTEGER(KIND=JPIM) :: ISTATUS = 0
  INTEGER(KIND=JPIM) :: LAST_CONTIGUOUS_DIMENSION = 0

  TYPE(GPU_STATS) :: STATS

  LOGICAL :: LOBJECT_COPIED = .FALSE.

CONTAINS
  PROCEDURE (SYNC), DEFERRED :: SYNC_HOST_RDWR
  PROCEDURE (SYNC), DEFERRED :: SYNC_HOST_RDONLY
  PROCEDURE (SYNC), DEFERRED :: SYNC_DEVICE_RDWR
  PROCEDURE (SYNC), DEFERRED :: SYNC_DEVICE_RDONLY
END TYPE FIELD_BASIC

ABSTRACT INTERFACE
  SUBROUTINE SYNC (SELF, QUEUE)
    IMPORT FIELD_BASIC
    CLASS(FIELD_BASIC), INTENT (INOUT) :: SELF
    INTEGER, OPTIONAL,  INTENT(IN)     :: QUEUE
  END SUBROUTINE
END INTERFACE


#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
TYPE, ABSTRACT, EXTENDS (FIELD_BASIC) :: ${field_type_name}$
  ! A FIELD encapsulates a single multi-dimensional array and can
  ! provide block-indexed "views" of the data for automating the
  ! allocation and parallel iterration of NPROMA blocks.

  ! TODO: Atlas-based field data storage field
  ! TODO: Do we still need to use pointers here?
  ! TYPE(ATLAS_FIELD), POINTER :: DATA

  ! Storage pointer for non-Atlas backward-compatibility mode
  !
  ! The underlying storage pointer has the rank as the dimension,
  ! where the innermost dimension represents the horizontal and
  ! the outermost one is the block index.
  ${dtype}$, POINTER :: PTR(${shapes[rank]}$) => NULL()

  ! A separate data pointer that can be used to create
  ! a contiguous chunk of host memory to cleanly map to
  ! device, should the %DATA pointer be discontiguous.
  ${dtype}$, POINTER, CONTIGUOUS :: DEVPTR(${shapes[rank]}$) => NULL()

CONTAINS

  PROCEDURE :: FINAL => ${field_type_name}$_FINAL
  PROCEDURE :: ${field_type_name}$_FINAL
  PROCEDURE :: DELETE_DEVICE => ${field_type_name}$_DELETE_DEVICE
  PROCEDURE :: GET_VIEW => ${field_type_name}$_GET_VIEW
  PROCEDURE :: GET_DEVICE_DATA_RDONLY => ${field_type_name}$_GET_DEVICE_DATA_RDONLY
  PROCEDURE :: GET_DEVICE_DATA_RDWR => ${field_type_name}$_GET_DEVICE_DATA_RDWR
  PROCEDURE :: GET_HOST_DATA_RDONLY => ${field_type_name}$_GET_HOST_DATA_RDONLY
  PROCEDURE :: GET_HOST_DATA_RDWR => ${field_type_name}$_GET_HOST_DATA_RDWR
  PROCEDURE :: SYNC_HOST_RDWR => ${field_type_name}$_SYNC_HOST_RDWR
  PROCEDURE :: SYNC_HOST_RDONLY => ${field_type_name}$_SYNC_HOST_RDONLY
  PROCEDURE :: SYNC_DEVICE_RDWR => ${field_type_name}$_SYNC_DEVICE_RDWR
  PROCEDURE :: SYNC_DEVICE_RDONLY => ${field_type_name}$_SYNC_DEVICE_RDONLY
  PROCEDURE :: COPY_OBJECT => ${field_type_name}$_COPY_OBJECT
  PROCEDURE :: WIPE_OBJECT => ${field_type_name}$_WIPE_OBJECT

  PROCEDURE, PRIVATE :: GET_DEVICE_DATA => ${field_type_name}$_GET_DEVICE_DATA
  PROCEDURE, PRIVATE :: GET_HOST_DATA => ${field_type_name}$_GET_HOST_DATA
  PROCEDURE, PRIVATE :: ${field_type_name}$_GET_HOST_DATA
  PROCEDURE, PRIVATE :: COPY_DATA =>  ${field_type_name}$_COPY_DATA
  PROCEDURE, PRIVATE :: CREATE_DEVICE_DATA => ${field_type_name}$_CREATE_DEVICE_DATA
END TYPE ${field_type_name}$

TYPE, EXTENDS(${field_type_name}$) :: ${field_type_name}$_WRAPPER
CONTAINS
  PROCEDURE :: INIT => ${field_type_name}$_WRAP
  PROCEDURE :: FINAL => ${field_type_name}$_WRAPPER_FINAL
END TYPE ${field_type_name}$_WRAPPER

TYPE, EXTENDS(${field_type_name}$) :: ${field_type_name}$_OWNER
  INTEGER(KIND=JPIM) :: LBOUNDS(${rank}$), UBOUNDS(${rank}$)
CONTAINS
  PROCEDURE :: INIT => ${field_type_name}$_OWNER_INIT
  PROCEDURE :: FINAL => ${field_type_name}$_OWNER_FINAL
  PROCEDURE, PRIVATE :: ALLOCATE => ${field_type_name}$_ALLOCATE
  PROCEDURE, PRIVATE :: GET_HOST_DATA => ${field_type_name}$_OWNER_GET_HOST_DATA
END TYPE ${field_type_name}$_OWNER

TYPE ${field_type_name}$_PTR
  ! Struct to hold references to field objects
  CLASS(${field_type_name}$), POINTER :: PTR => NULL()
END TYPE ${field_type_name}$_PTR

TYPE ${field_type_name}$_VIEW
  ! Struct to hold array views, so we can make arrays of them
  ${dtype}$, POINTER :: P(${shapes[rank-1]}$) => NULL()
END TYPE ${field_type_name}$_VIEW

#:endfor

CONTAINS
!
! CLASS METHODS
!
#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  SUBROUTINE ${field_type_name}$_WRAP(SELF, DATA, PERSISTENT, LBOUNDS)
    ! Create FIELD object by wrapping existing data
    CLASS(${field_type_name}$_WRAPPER), INTENT(INOUT) :: SELF
    ${dtype}$, TARGET, INTENT(IN) :: DATA(${shapes[rank]}$)
    LOGICAL, INTENT(IN), OPTIONAL :: PERSISTENT
    INTEGER(KIND=JPIM), INTENT(IN), OPTIONAL :: LBOUNDS(${rank}$)
    LOGICAL :: LLPERSISTENT

    LLPERSISTENT = .TRUE.
    IF (PRESENT (PERSISTENT)) LLPERSISTENT = PERSISTENT

    IF (PRESENT(LBOUNDS)) THEN
      SELF%PTR(${lbptr (rank)}$) => DATA
    ELSE
      SELF%PTR => DATA
    ENDIF
    SELF%THREAD_BUFFER = .NOT. LLPERSISTENT
    SELF%ISTATUS = NHSTFRESH

    IF (.NOT. LLPERSISTENT) THEN
      IF (OMP_GET_MAX_THREADS() /= SIZE (DATA, ${rank}$)) THEN
        CALL ABOR1 ('${field_type_name}$_WRAP: DIMENSION MISMATCH')
      ENDIF
    ENDIF

  END SUBROUTINE ${field_type_name}$_WRAP

  SUBROUTINE ${field_type_name}$_OWNER_INIT (SELF, LBOUNDS, UBOUNDS, PERSISTENT, DELAYED)
    CLASS(${field_type_name}$_OWNER) :: SELF
    INTEGER(KIND=JPIM), INTENT(IN) :: LBOUNDS(${rank}$)
    INTEGER(KIND=JPIM), INTENT(IN) :: UBOUNDS(${rank}$)
    LOGICAL, OPTIONAL,  INTENT(IN) :: PERSISTENT
    LOGICAL, OPTIONAL,  INTENT(IN) :: DELAYED
    LOGICAL :: IS_DELAYED

    IS_DELAYED = .FALSE.
    IF(PRESENT(DELAYED))THEN
      IS_DELAYED = DELAYED
    ENDIF

    SELF%LBOUNDS=LBOUNDS
    SELF%UBOUNDS=UBOUNDS
    SELF%UBOUNDS(${rank}$) = OMP_GET_MAX_THREADS()

    ! By default we allocate thread-local temporaries
    SELF%THREAD_BUFFER = .TRUE.

    SELF%LAST_CONTIGUOUS_DIMENSION = ${rank}$

    IF (PRESENT(PERSISTENT)) THEN
      IF (PERSISTENT) THEN
        SELF%THREAD_BUFFER = .FALSE.
        SELF%LBOUNDS(${rank}$) = 1
        SELF%UBOUNDS(${rank}$) = UBOUNDS(${rank}$)
      END IF
    END IF

    SELF%ISTATUS = UNALLOCATED
    IF(.NOT. IS_DELAYED) THEN
      CALL SELF%ALLOCATE()
    ENDIF
  END SUBROUTINE ${field_type_name}$_OWNER_INIT

  SUBROUTINE ${field_type_name}$_ALLOCATE (SELF)
    ! Create FIELD object by explicitly allocating new data
    CLASS(${field_type_name}$_OWNER) :: SELF

    ALLOCATE(SELF%PTR(${', '.join(map(lambda r: 'SELF%LBOUNDS('+str(r+1)+'):SELF%UBOUNDS('+str(r+1)+')', range(0, rank)))}$))
    SELF%ISTATUS = NHSTFRESH
  END SUBROUTINE ${field_type_name}$_ALLOCATE

  FUNCTION ${field_type_name}$_GET_VIEW(SELF, BLOCK_INDEX, ZERO) RESULT(VIEW_PTR)
    CLASS(${field_type_name}$) :: SELF
    ${dtype}$, POINTER :: VIEW_PTR(${shapes[rank-1]}$)
    INTEGER(KIND=JPIM), INTENT(IN) :: BLOCK_INDEX
    LOGICAL, OPTIONAL, INTENT(IN) :: ZERO
    INTEGER(KIND=JPIM) :: IDX
    INTEGER(KIND=JPIM) :: LBOUNDS(${rank}$)

    IDX = BLOCK_INDEX
    IF (SELF%THREAD_BUFFER) IDX = OMP_GET_THREAD_NUM()

    LBOUNDS=LBOUND(SELF%PTR)
    VIEW_PTR(${','.join('LBOUNDS(%d):'%(r+1) for r in range(rank-1))}$) => SELF%PTR(${','.join(':' for _ in range(rank-1))}$,IDX)

    IF (PRESENT(ZERO)) THEN
      IF (ZERO) VIEW_PTR(${shapes[rank-1]}$) = ${default}$
    END IF
  END FUNCTION ${field_type_name}$_GET_VIEW

  SUBROUTINE ${field_type_name}$_DELETE_DEVICE(SELF)
    ! Delete the copy of this field on GPU device
    CLASS(${field_type_name}$) :: SELF

    IF (ASSOCIATED (SELF%DEVPTR)) THEN
#ifdef _OPENACC
      !$acc exit data delete (SELF%DEVPTR)
#endif /* _OPENACC */
      DEALLOCATE (SELF%DEVPTR)
      NULLIFY (SELF%DEVPTR)
    ENDIF
  END SUBROUTINE ${field_type_name}$_DELETE_DEVICE

  SUBROUTINE ${field_type_name}$_FINAL(SELF)
    ! Finalizes field and deallocates owned data
    CLASS(${field_type_name}$) :: SELF
    NULLIFY(SELF%PTR)
    CALL SELF%DELETE_DEVICE()
  END SUBROUTINE ${field_type_name}$_FINAL

  SUBROUTINE ${field_type_name}$_WRAPPER_FINAL(SELF)
    ! Finalizes field and deallocates owned data
    CLASS(${field_type_name}$_WRAPPER) :: SELF
    ${dtype}$, POINTER :: PTR(${shapes[rank]}$)
    CALL SELF%GET_HOST_DATA_RDONLY(PTR)
    CALL SELF%${field_type_name}$_FINAL
  END SUBROUTINE ${field_type_name}$_WRAPPER_FINAL

  SUBROUTINE ${field_type_name}$_OWNER_FINAL(SELF)
    ! Finalizes field and deallocates owned data
    CLASS(${field_type_name}$_OWNER) :: SELF
    IF (ASSOCIATED(SELF%PTR)) THEN
      DEALLOCATE(SELF%PTR)
    END IF
    CALL SELF%${field_type_name}$_FINAL
  END SUBROUTINE ${field_type_name}$_OWNER_FINAL

  SUBROUTINE ${field_type_name}$_COPY_OBJECT (SELF, LDCREATED)
  CLASS(${field_type_name}$) :: SELF
  LOGICAL, INTENT (IN), OPTIONAL :: LDCREATED
  LOGICAL :: LLCREATED

  LLCREATED = .FALSE.
  IF (PRESENT (LDCREATED)) LLCREATED = LDCREATED

  IF (.NOT. LLCREATED) STOP 1

  IF (.NOT. SELF%LOBJECT_COPIED) THEN
    SELF%LOBJECT_COPIED = .TRUE.
#ifdef _OPENACC
    IF (ASSOCIATED (SELF%DEVPTR)) THEN
      !$acc enter data attach (SELF%DEVPTR)

    ENDIF
#endif
  ENDIF

  END SUBROUTINE ${field_type_name}$_COPY_OBJECT

  SUBROUTINE ${field_type_name}$_WIPE_OBJECT (SELF, LDDELETED)
  CLASS(${field_type_name}$) :: SELF
  LOGICAL, INTENT (IN), OPTIONAL :: LDDELETED
  LOGICAL :: LLDELETED

  LLDELETED = .FALSE.
  IF (PRESENT (LDDELETED)) LLDELETED = LDDELETED

  IF (.NOT. LLDELETED) STOP 1

  IF (SELF%LOBJECT_COPIED) THEN
    SELF%LOBJECT_COPIED = .FALSE.
#ifdef _OPENACC
    IF (ASSOCIATED (SELF%DEVPTR)) THEN
      !$acc exit data detach (SELF%DEVPTR)
    ENDIF
#endif
  ENDIF

  END SUBROUTINE ${field_type_name}$_WIPE_OBJECT

  SUBROUTINE ${field_type_name}$_COPY_DATA (SELF, KDIR, QUEUE)
  CLASS(${field_type_name}$)                :: SELF
  INTEGER (KIND=JPIM),           INTENT(IN) :: KDIR
  INTEGER (KIND=JPIM), OPTIONAL, INTENT(IN) :: QUEUE
  REAL :: START, FINISH

  SELF%LAST_CONTIGUOUS_DIMENSION = ${field_type_name}$_GET_LAST_CONTIGUOUS_DIMENSION (SELF%PTR)

  CALL CPU_TIME(START)
  SELECT CASE (SELF%LAST_CONTIGUOUS_DIMENSION)
#:for d in range (rank + 1)
    CASE (${d}$)
      CALL COPY_DIM${d}$_CONTIGUOUS (SELF%PTR, SELF%DEVPTR, KDIR, QUEUE)
#:endfor
  END SELECT
  CALL CPU_TIME(FINISH)
  IF (KDIR == NH2D) THEN
    CALL SELF%STATS%INC_CPU_TO_GPU_TRANSFER(START, FINISH)
  ELSE IF (KDIR == ND2H) THEN
    CALL SELF%STATS%INC_GPU_TO_CPU_TRANSFER(START, FINISH)
  END IF

  CONTAINS

#:for d in range (0, rank+1)
    SUBROUTINE COPY_DIM${d}$_CONTIGUOUS (HST, DEV, KDIR, QUEUE)
#ifdef _OPENACC
    USE OPENACC
#endif
    ${dtype}$, POINTER :: HST (${shapes[rank]}$), DEV (${shapes[rank]}$)
    INTEGER (KIND=JPIM),           INTENT (IN) :: KDIR
    INTEGER (KIND=JPIM), OPTIONAL, INTENT (IN) :: QUEUE
    INTEGER (KIND=JPIM) :: ISIZE
    INTEGER :: ${', '.join (['J'] + list (map (lambda i: 'J' + str (i+1), range (d, rank))))}$

  #:for e in range (rank, d, -1)
    ${'  ' * (rank - e)}$DO J${e}$ = LBOUND (HST, ${e}$), UBOUND (HST, ${e}$)
  #:endfor
  #:set ar = ', '.join ([':'] * d  + list (map (lambda i: 'J' + str (i+1), range (d, rank))))
  #:set indent = '  ' * (rank - e)
    #:if d == 0
    ${indent}$  ISIZE = KIND (HST)
    #:else
    ${indent}$  ISIZE = KIND (HST) * SIZE (HST (${ar}$))
    #:endif
    ${indent}$  IF (KDIR == NH2D) THEN
#ifdef _OPENACC
    ${indent}$    !$acc host_data use_device (DEV)
    ${indent}$    IF(PRESENT(QUEUE))THEN
    ${indent}$      CALL ACC_MEMCPY_TO_DEVICE_ASYNC (DEV (${ar}$), HST (${ar}$), ISIZE, QUEUE)
    ${indent}$    ELSE
    ${indent}$      CALL ACC_MEMCPY_TO_DEVICE (DEV (${ar}$), HST (${ar}$), ISIZE)
    ${indent}$    ENDIF
    ${indent}$    !$acc end host_data
#else
    ${indent}$    DEV (${ar}$) = HST (${ar}$)
#endif
    ${indent}$  ELSEIF (KDIR == ND2H) THEN
#ifdef _OPENACC
    ${indent}$    !$acc host_data use_device (DEV)
    ${indent}$    IF(QUEUE == -1)THEN
    ${indent}$      CALL ACC_MEMCPY_FROM_DEVICE (HST (${ar}$), DEV (${ar}$), ISIZE)
    ${indent}$    ELSE
    ${indent}$      CALL ACC_MEMCPY_FROM_DEVICE_ASYNC (HST (${ar}$), DEV (${ar}$), ISIZE, QUEUE)
    ${indent}$    ENDIF
    ${indent}$    !$acc end host_data
#else
    ${indent}$    HST (${ar}$) = DEV (${ar}$)
#endif
    ${indent}$  ENDIF
  #:for e in range (d, rank)
    ${'  ' * (rank - e - 1)}$ENDDO
  #:endfor
    END SUBROUTINE

#:endfor

  END SUBROUTINE ${field_type_name}$_COPY_DATA


  SUBROUTINE ${field_type_name}$_GET_HOST_DATA (SELF, MODE, PTR, QUEUE)
    CLASS(${field_type_name}$),    INTENT(INOUT) :: SELF
    INTEGER (KIND=JPIM),           INTENT(IN)    :: MODE

    ${dtype}$, POINTER,            INTENT(INOUT) :: PTR(${shapes[rank]}$)
    INTEGER (KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE

    INTEGER(KIND=JPIM) :: LBOUNDS(${rank}$)

    LBOUNDS=LBOUND(SELF%PTR)
    IF (IAND (SELF%ISTATUS, NHSTFRESH) == 0) THEN
      CALL SELF%COPY_DATA (ND2H, QUEUE)
      SELF%ISTATUS = IOR (SELF%ISTATUS, NHSTFRESH)
    ENDIF
    PTR (${lbptr (rank)}$) => SELF%PTR (${','.join(':' for _ in range(rank))}$)
    IF (IAND (MODE, NWR) /= 0) THEN
      SELF%ISTATUS = IAND (SELF%ISTATUS, NOT (NDEVFRESH))
    ENDIF

  END SUBROUTINE ${field_type_name}$_GET_HOST_DATA

  SUBROUTINE ${field_type_name}$_OWNER_GET_HOST_DATA (SELF, MODE, PTR, QUEUE)
    CLASS(${field_type_name}$_OWNER), INTENT(INOUT) :: SELF
    INTEGER (KIND=JPIM),           INTENT(IN)    :: MODE
    ${dtype}$, POINTER,            INTENT(INOUT) :: PTR(${shapes[rank]}$)
    INTEGER (KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE

    IF(SELF%ISTATUS==UNALLOCATED)THEN
      CALL SELF%ALLOCATE()
    ENDIF
    CALL SELF%${field_type_name}$_GET_HOST_DATA(MODE, PTR, QUEUE)

  END SUBROUTINE ${field_type_name}$_OWNER_GET_HOST_DATA

  SUBROUTINE ${field_type_name}$_GET_HOST_DATA_RDONLY (SELF, PPTR, QUEUE)
    CLASS(${field_type_name}$),   INTENT(INOUT) :: SELF
    ${dtype}$, POINTER,           INTENT(INOUT) :: PPTR(${shapes[rank]}$)
    INTEGER(KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE

    CALL SELF%GET_HOST_DATA (NRD, PPTR, QUEUE)

  END SUBROUTINE ${field_type_name}$_GET_HOST_DATA_RDONLY

  SUBROUTINE ${field_type_name}$_SYNC_HOST_RDONLY (SELF, QUEUE)
    CLASS(${field_type_name}$),   INTENT(INOUT) :: SELF
    INTEGER(KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE
    ${dtype}$, POINTER                          :: ZPTR(${shapes[rank]}$)

    IF(PRESENT(QUEUE))THEN
      CALL SELF%GET_HOST_DATA_RDONLY (ZPTR, QUEUE=QUEUE)
    ELSE
      CALL SELF%GET_HOST_DATA_RDONLY (ZPTR)
    ENDIF

  END SUBROUTINE ${field_type_name}$_SYNC_HOST_RDONLY

  SUBROUTINE ${field_type_name}$_GET_HOST_DATA_RDWR (SELF, PPTR, QUEUE)
    CLASS(${field_type_name}$),   INTENT(INOUT) :: SELF
    ${dtype}$, POINTER,           INTENT(INOUT) :: PPTR(${shapes[rank]}$)
    INTEGER(KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE

    CALL SELF%GET_HOST_DATA (IOR (NRD, NWR), PPTR, QUEUE)

  END SUBROUTINE ${field_type_name}$_GET_HOST_DATA_RDWR

  SUBROUTINE ${field_type_name}$_SYNC_HOST_RDWR (SELF, QUEUE)
    CLASS(${field_type_name}$),   INTENT(INOUT) :: SELF
    INTEGER(KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE
    ${dtype}$, POINTER                          :: ZPTR(${shapes[rank]}$)

    IF(PRESENT(QUEUE))THEN
      CALL SELF%GET_HOST_DATA_RDWR (ZPTR, QUEUE=QUEUE)
    ELSE
      CALL SELF%GET_HOST_DATA_RDWR (ZPTR)
    ENDIF

  END SUBROUTINE ${field_type_name}$_SYNC_HOST_RDWR

  SUBROUTINE ${field_type_name}$_CREATE_DEVICE_DATA (SELF)
    CLASS(${field_type_name}$), INTENT (INOUT) :: SELF
#if __INTEL_COMPILER == 1800 && __INTEL_COMPILER_UPDATE == 5
! Bug with Intel 18.0.5.274
    ALLOCATE (SELF%DEVPTR (${ ', '.join (map (lambda i: 'LBOUND (SELF%PTR, ' + str (i) + '):UBOUND (SELF%PTR,' + str (i) + ')', range (1, rank+1))) }$))
#else
    ALLOCATE (SELF%DEVPTR, MOLD=SELF%PTR)
#endif
#ifdef _OPENACC
    !$acc enter data create (SELF%DEVPTR)
    IF (SELF%LOBJECT_COPIED) THEN
      !$acc enter data attach (SELF%DEVPTR)
    ENDIF
#endif
  END SUBROUTINE

  SUBROUTINE ${field_type_name}$_GET_DEVICE_DATA (SELF, MODE, PTR, QUEUE)
    CLASS(${field_type_name}$),    INTENT(INOUT) :: SELF
    INTEGER (KIND=JPIM),           INTENT(IN)    :: MODE
    ${dtype}$, POINTER,            INTENT(INOUT) :: PTR(${shapes[rank]}$)
    INTEGER (KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE
    INTEGER(KIND=JPIM)                           :: LBOUNDS(${rank}$)

    LBOUNDS=LBOUND(SELF%PTR)
    IF (IAND (SELF%ISTATUS, NDEVFRESH) == 0) THEN
      IF (.NOT. ASSOCIATED (SELF%DEVPTR)) THEN
        CALL SELF%CREATE_DEVICE_DATA
      ENDIF
      CALL SELF%COPY_DATA (NH2D, QUEUE)
      SELF%ISTATUS = IOR (SELF%ISTATUS, NDEVFRESH)
    ENDIF
    PTR (${lbptr (rank)}$) => SELF%DEVPTR (${','.join(':' for _ in range(rank))}$)
    IF (IAND (MODE, NWR) /= 0) THEN
      SELF%ISTATUS = IAND (SELF%ISTATUS, NOT (NHSTFRESH))
    ENDIF

  END SUBROUTINE ${field_type_name}$_GET_DEVICE_DATA

  SUBROUTINE ${field_type_name}$_GET_DEVICE_DATA_RDONLY (SELF, PPTR, QUEUE)
    CLASS(${field_type_name}$),   INTENT(INOUT) :: SELF
    ${dtype}$, POINTER,           INTENT(INOUT) :: PPTR(${shapes[rank]}$)
    INTEGER(KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE

    CALL ${field_type_name}$_GET_DEVICE_DATA (SELF, NRD, PPTR, QUEUE)

  END SUBROUTINE ${field_type_name}$_GET_DEVICE_DATA_RDONLY

  SUBROUTINE ${field_type_name}$_SYNC_DEVICE_RDONLY (SELF, QUEUE)
    CLASS(${field_type_name}$),   INTENT(INOUT) :: SELF
    INTEGER(KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE
    ${dtype}$, POINTER                          :: ZPTR(${shapes[rank]}$)

    IF(PRESENT(QUEUE))THEN
      CALL SELF%GET_DEVICE_DATA_RDONLY (ZPTR, QUEUE)
    ELSE
      CALL SELF%GET_DEVICE_DATA_RDONLY (ZPTR)
    ENDIF

  END SUBROUTINE ${field_type_name}$_SYNC_DEVICE_RDONLY

  SUBROUTINE ${field_type_name}$_GET_DEVICE_DATA_RDWR (SELF, PPTR, QUEUE)
    CLASS(${field_type_name}$),   INTENT(INOUT) :: SELF
    ${dtype}$, POINTER,           INTENT(INOUT) :: PPTR(${shapes[rank]}$)
    INTEGER(KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE

    CALL ${field_type_name}$_GET_DEVICE_DATA (SELF, IOR (NRD, NWR), PPTR, QUEUE)

  END SUBROUTINE ${field_type_name}$_GET_DEVICE_DATA_RDWR

  SUBROUTINE ${field_type_name}$_SYNC_DEVICE_RDWR (SELF, QUEUE)
    CLASS(${field_type_name}$),   INTENT(INOUT) :: SELF
    INTEGER(KIND=JPIM), OPTIONAL, INTENT(IN)    :: QUEUE
    ${dtype}$, POINTER                          :: ZPTR(${shapes[rank]}$)

    IF(PRESENT(QUEUE))THEN
      CALL SELF%GET_DEVICE_DATA_RDWR (ZPTR, QUEUE)
    ELSE
      CALL SELF%GET_DEVICE_DATA_RDWR (ZPTR)
    ENDIF

  END SUBROUTINE ${field_type_name}$_SYNC_DEVICE_RDWR

#:endfor

  SUBROUTINE INC_CPU_TO_GPU_TRANSFER(SELF, START, FINISH)
    CLASS(GPU_STATS), INTENT(INOUT) :: SELF
    REAL, INTENT(IN) :: START, FINISH
    SELF%TRANSFER_CPU_TO_GPU = SELF%TRANSFER_CPU_TO_GPU + 1
    SELF%TOTAL_TIME_TRANSFER_CPU_TO_GPU = SELF%TOTAL_TIME_TRANSFER_CPU_TO_GPU + FINISH - START
  END SUBROUTINE

  SUBROUTINE INC_GPU_TO_CPU_TRANSFER(SELF, START, FINISH)
    CLASS(GPU_STATS), INTENT(INOUT) :: SELF
    REAL, INTENT(IN) :: START, FINISH
    SELF%TRANSFER_GPU_TO_CPU = SELF%TRANSFER_GPU_TO_CPU + 1
    SELF%TOTAL_TIME_TRANSFER_GPU_TO_CPU = SELF%TOTAL_TIME_TRANSFER_GPU_TO_CPU + FINISH - START
  END SUBROUTINE


!
! HELPERS
!

#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  INTEGER (KIND=JPIM) FUNCTION ${field_type_name}$_GET_LAST_CONTIGUOUS_DIMENSION (PTR) RESULT (JDIM)
  ${dtype}$, POINTER :: PTR (${shapes[rank]}$)
  INTEGER*8 :: ISTRIDE (${rank}$)
  INTEGER (KIND=JPIM) :: J

  ISTRIDE (1) = KIND (PTR)
  DO J = 2, ${rank}$
    ISTRIDE (J) = ISTRIDE (J-1) * SIZE (PTR, J-1)
  ENDDO

  JDIM = 0
#:for d in range (1, rank+1)
  #:set ind0 = ['1'] * rank
  #:set ind1 = ['1'] * (d - 1) + ['2'] + ['1'] * (rank - d)
  IF (LOC (PTR (${', '.join (ind1)}$)) - LOC (PTR (${', '.join (ind0)}$)) /= ISTRIDE (${d}$)) THEN
    RETURN
  ENDIF

  JDIM = ${d}$

#:endfor
  END FUNCTION ${field_type_name}$_GET_LAST_CONTIGUOUS_DIMENSION
#:endfor

  SUBROUTINE WAIT_FOR_ASYNC_QUEUE(QUEUE)
    INTEGER(KIND=JPIM), intent(in) :: queue
    !Wait for all data transfer initiated on queue by the current thread
!$acc wait (queue)
  END SUBROUTINE WAIT_FOR_ASYNC_QUEUE


END MODULE FIELD_MODULE
