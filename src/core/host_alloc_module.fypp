#! (C) Copyright 2022- ECMWF.
#! (C) Copyright 2022- Meteo-France.
#!
#! This software is licensed under the terms of the Apache Licence Version 2.0
#! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
#! In applying this licence, ECMWF does not waive the privileges and immunities
#! granted to it by virtue of its status as an intergovernmental organisation
#! nor does it submit to any jurisdiction.

MODULE HOST_ALLOC_MODULE

#:set fieldTypeList = fieldType.getFieldTypeList ()

${fieldType.useParkind1 ()}$
USE, INTRINSIC::ISO_C_BINDING
USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY : INT64
USE FIELD_DEFAULTS_MODULE
USE FIELD_STATISTICS_MODULE
USE FIELD_ABORT_MODULE
USE MEM_POOL_MODULE, ONLY: MEM_POOL, HST_MEM_BLK_INIT, HST_MEM_BLK_FINAL, HOST_MEM_BLOCK

IMPLICIT NONE

PRIVATE

INTERFACE HOST_ALLOC
#:for ft in fieldTypeList
  MODULE PROCEDURE ${ft.name}$_HOST_ALLOC
#:endfor
END INTERFACE HOST_ALLOC

INTERFACE HOST_ALLOC_POOLED
#:for ft in fieldTypeList
  MODULE PROCEDURE ${ft.name}$_HOST_ALLOC_POOLED
#:endfor
END INTERFACE HOST_ALLOC_POOLED

INTERFACE HOST_DEALLOC
#:for ft in fieldTypeList
  MODULE PROCEDURE ${ft.name}$_HOST_DEALLOC
#:endfor
END INTERFACE HOST_DEALLOC

INTERFACE HOST_DEALLOC_POOLED
#:for ft in fieldTypeList
  MODULE PROCEDURE ${ft.name}$_HOST_DEALLOC_POOLED
#:endfor
END INTERFACE HOST_DEALLOC_POOLED

INTERFACE
  SUBROUTINE C_MALLOC (SIZ, PTR) BIND (C, NAME='c_malloc')
    IMPORT :: C_PTR, C_SIZE_T
    INTEGER (C_SIZE_T), VALUE, INTENT(IN) :: SIZ
    TYPE (C_PTR), INTENT(OUT) :: PTR
  END SUBROUTINE C_MALLOC
  SUBROUTINE C_FREE (PTR) BIND (C, NAME='c_free')
    IMPORT :: C_PTR
    TYPE (C_PTR), VALUE, INTENT(IN) :: PTR
  END SUBROUTINE C_FREE
END INTERFACE

#:if defined('WITH_HIC')
INTERFACE
$:offload_macros.host_register_intf(indent=3)
$:offload_macros.host_unregister_intf(indent=3)
END INTERFACE
#:endif 

TYPE(MEM_POOL) :: HOST_POOL

PUBLIC :: HOST_ALLOC
PUBLIC :: HOST_ALLOC_POOLED
PUBLIC :: HOST_DEALLOC
PUBLIC :: HOST_DEALLOC_POOLED
PUBLIC :: HOST_POOL

CONTAINS

SUBROUTINE MEM_BLK_INIT( SIZE, DATA, LPINNED )
   INTEGER(C_SIZE_T), INTENT(IN) :: SIZE
   TYPE(C_PTR), INTENT(OUT) :: DATA
   LOGICAL, INTENT(OUT) :: LPINNED

   CALL C_MALLOC(SIZE, DATA)
 
   LPINNED = INIT_PINNED_VALUE
#:if defined('WITH_HIC')
   IF(LPINNED)THEN
     CALL PIN_ALLOCATION(DATA, SIZE)
   ENDIF
#:endif

END SUBROUTINE MEM_BLK_INIT

SUBROUTINE MEM_BLK_FINAL( DATA, LPINNED )
   TYPE(C_PTR), INTENT(INOUT) :: DATA
   LOGICAL, INTENT(IN) :: LPINNED

#:if defined('WITH_HIC')
   IF (LPINNED) THEN
     CALL UNPIN_ALLOCATION(DATA)
   ENDIF
#:endif

   CALL C_FREE(DATA)
END SUBROUTINE MEM_BLK_FINAL

#:if defined('WITH_HIC')
SUBROUTINE PIN_ALLOCATION(DATA, ARR_SIZE)

   TYPE(C_PTR), INTENT(INOUT) :: DATA
   INTEGER(C_SIZE_T), INTENT(IN) :: ARR_SIZE
$:offload_macros.runtime_error_decl(symbols=['ISTAT',], indent=3)

$:offload_macros.register_host_decl_flags(flag_var='FLAGS', indent=3)

$:offload_macros.register_host_set_flags(flag_var='FLAGS', val='2', indent=3)
$:offload_macros.register_host(ptr='DATA', size='ARR_SIZE', flags='FLAGS', return_val='ISTAT')

   IF (ISTAT /= 0) THEN
     CALL FIELD_ABORT ("FAILED TO REGISTER IN PAGE-LOCKED MEMORY")
   ENDIF

END SUBROUTINE PIN_ALLOCATION

SUBROUTINE UNPIN_ALLOCATION(DATA)

   TYPE(C_PTR), INTENT(INOUT) :: DATA
$:offload_macros.runtime_error_decl(symbols=['ISTAT',], indent=3)

$:offload_macros.unregister_host(ptr='DATA', return_val='ISTAT', indent=3)

   IF (ISTAT /= 0) THEN
     CALL FIELD_ABORT ("FAILED TO UNREGISTER PAGE-LOCKED MEMORY")
   ENDIF

END SUBROUTINE UNPIN_ALLOCATION
#:endif

#:for ft in fieldTypeList
SUBROUTINE ${ft.name}$_HOST_ALLOC_POOLED (HST, LBOUNDS, UBOUNDS, BLKID)

   ${ft.type}$, POINTER, INTENT(OUT) :: HST(${ft.shape}$)
   INTEGER(KIND=JPIM), INTENT(IN) :: LBOUNDS(${ft.rank}$)
   INTEGER(KIND=JPIM), INTENT(IN) :: UBOUNDS(${ft.rank}$)
   INTEGER(KIND=JPIM), INTENT(INOUT) :: BLKID

   ${ft.type}$, POINTER :: PTR(${ft.shape}$)
   TYPE(C_PTR) :: DATA
   INTEGER(C_SIZE_T) :: ARR_SIZE
   INTEGER :: ISHAPE(${ft.rank}$), ISTAT
   CHARACTER(LEN=20) :: HOST_POOL_BLOCK_SIZE
   INTEGER(C_SIZE_T) :: HOST_POOL_BLOCK_SIZE_INT

   ARR_SIZE = KIND(HST)
#:for r in range(ft.rank)
   ISHAPE(${r+1}$) = UBOUNDS(${r+1}$) - LBOUNDS(${r+1}$) + 1
   ARR_SIZE = ARR_SIZE * ISHAPE(${r+1}$)
#:endfor

   IF ( .NOT. ASSOCIATED(HOST_POOL%START_BLK) ) THEN
      ALLOCATE(HOST_MEM_BLOCK::HOST_POOL%START_BLK)
      HST_MEM_BLK_INIT => MEM_BLK_INIT
      HST_MEM_BLK_FINAL => MEM_BLK_FINAL

      CALL GET_ENVIRONMENT_VARIABLE('FIELD_API_HOST_POOL_BLOCK_SIZE', HOST_POOL_BLOCK_SIZE, STATUS=ISTAT)
      IF (ISTAT == 0) THEN
         HOST_POOL_BLOCK_SIZE = TRIM(HOST_POOL_BLOCK_SIZE)
         READ(HOST_POOL_BLOCK_SIZE, *) HOST_POOL_BLOCK_SIZE_INT
         IF (HOST_POOL_BLOCK_SIZE_INT > 0) POOL_BLOCK_SIZE = HOST_POOL_BLOCK_SIZE_INT
      ENDIF
   ENDIF

   IF(ARR_SIZE > 0)THEN
     CALL HOST_POOL%ALLOC(ARR_SIZE, BLKID, DATA)
     CALL C_F_POINTER(DATA, PTR, SHAPE=ISHAPE)

     HST(${', '.join(map(lambda r: 'LBOUNDS('+str(r+1)+'):', range(0, ft.rank)))}$) => PTR
   ELSE
     ALLOCATE(HST(${','.join([f'LBOUNDS({r+1}):UBOUNDS({r+1})' for r in range(ft.rank)])}$))
   ENDIF

   IF (FIELD_STATISTICS_ENABLE) CALL FIELD_STATISTICS_HOST_ALLOCATE (SIZE (HST, KIND=JPIB) * INT (KIND (HST), KIND=JPIB))

END SUBROUTINE ${ft.name}$_HOST_ALLOC_POOLED

SUBROUTINE ${ft.name}$_HOST_ALLOC (HST, LBOUNDS, UBOUNDS, PINNED)

   ${ft.type}$, POINTER, INTENT(OUT) :: HST(${ft.shape}$)
   INTEGER(KIND=JPIM), INTENT(IN) :: LBOUNDS(${ft.rank}$)
   INTEGER(KIND=JPIM), INTENT(IN) :: UBOUNDS(${ft.rank}$)
   LOGICAL, INTENT(IN) :: PINNED

   ${ft.type}$, POINTER :: PTR(${ft.shape}$)
   TYPE(C_PTR) :: DATA
   INTEGER(C_SIZE_T) :: ARR_SIZE
   INTEGER :: ISHAPE(${ft.rank}$)

   ARR_SIZE = KIND(HST)
#:for r in range(ft.rank)
   ISHAPE(${r+1}$) = UBOUNDS(${r+1}$) - LBOUNDS(${r+1}$) + 1
   ARR_SIZE = ARR_SIZE * ISHAPE(${r+1}$)
#:endfor

   IF(ARR_SIZE > 0)THEN
     CALL C_MALLOC(ARR_SIZE, DATA)

#:if defined('WITH_HIC')
     IF(PINNED)THEN
       CALL PIN_ALLOCATION(DATA, ARR_SIZE)
     ENDIF
#:endif

     CALL C_F_POINTER(DATA, PTR, SHAPE=ISHAPE)
     HST(${', '.join(map(lambda r: 'LBOUNDS('+str(r+1)+'):', range(0, ft.rank)))}$) => PTR
   ELSE
     ALLOCATE(HST(${','.join([f'LBOUNDS({r+1}):UBOUNDS({r+1})' for r in range(ft.rank)])}$))
   ENDIF

   IF (FIELD_STATISTICS_ENABLE) CALL FIELD_STATISTICS_HOST_ALLOCATE (SIZE (HST, KIND=JPIB) * INT (KIND (HST), KIND=JPIB))

END SUBROUTINE ${ft.name}$_HOST_ALLOC

SUBROUTINE ${ft.name}$_HOST_DEALLOC(HST, PINNED)

   ${ft.type}$, POINTER, INTENT(INOUT) :: HST(${ft.shape}$)
   LOGICAL, INTENT (IN) :: PINNED

   TYPE(C_PTR) :: DATA
  
   IF (FIELD_STATISTICS_ENABLE) CALL FIELD_STATISTICS_HOST_DEALLOCATE (SIZE (HST, KIND=JPIB) * INT (KIND (HST), KIND=JPIB))

   IF(SIZE(HST) > 0)THEN
     DATA = C_LOC (HST (${ ', '.join (map (lambda i: 'LBOUND (HST, ' + str (i) + ')', range (1, ft.rank+1))) }$))

#:if defined('WITH_HIC')
     IF (PINNED) THEN
       CALL UNPIN_ALLOCATION(DATA)
     ENDIF
#:endif

     CALL C_FREE(DATA)
   ELSE
     DEALLOCATE(HST)
   ENDIF

   NULLIFY(HST)

END SUBROUTINE ${ft.name}$_HOST_DEALLOC

SUBROUTINE ${ft.name}$_HOST_DEALLOC_POOLED(HST, BLKID)

   ${ft.type}$, POINTER, INTENT(INOUT) :: HST(${ft.shape}$)
   INTEGER(KIND=JPIM), INTENT(IN) :: BLKID

   TYPE(C_PTR) :: DATA
   INTEGER(C_SIZE_T) :: ARR_SIZE
   INTEGER :: ISHAPE(${ft.rank}$)
   INTEGER :: UBOUNDS(${ft.rank}$)
   INTEGER :: LBOUNDS(${ft.rank}$)

  UBOUNDS = UBOUND(HST)
  LBOUNDS = LBOUND(HST)

   ARR_SIZE = KIND(HST)
#:for r in range(ft.rank)
   ISHAPE(${r+1}$) = UBOUNDS(${r+1}$) - LBOUNDS(${r+1}$) + 1
   ARR_SIZE = ARR_SIZE * ISHAPE(${r+1}$)
#:endfor
  
   IF (FIELD_STATISTICS_ENABLE) CALL FIELD_STATISTICS_HOST_DEALLOCATE (SIZE (HST, KIND=JPIB) * INT (KIND (HST), KIND=JPIB))

   IF(ARR_SIZE > 0)THEN
     CALL HOST_POOL%DEALLOC(BLKID)
   ELSE
     DEALLOCATE(HST)
   ENDIF

   NULLIFY(HST)

END SUBROUTINE ${ft.name}$_HOST_DEALLOC_POOLED

#:endfor

END MODULE HOST_ALLOC_MODULE
