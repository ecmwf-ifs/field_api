! (C) Copyright 2022- ECMWF.
! (C) Copyright 2022- Meteo-France.
!
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.

MODULE FIELD_BASIC_MODULE

USE DEV_ALLOC_MODULE
USE FIELD_CONSTANTS_MODULE
USE PARKIND1, ONLY : JPIM

IMPLICIT NONE

PRIVATE

TYPE GPU_STATS
  INTEGER :: TRANSFER_CPU_TO_GPU = 0
  INTEGER :: TRANSFER_GPU_TO_CPU = 0
  REAL :: TOTAL_TIME_TRANSFER_CPU_TO_GPU = 0
  REAL :: TOTAL_TIME_TRANSFER_GPU_TO_CPU = 0
  CONTAINS
  PROCEDURE :: INC_CPU_TO_GPU_TRANSFER
  PROCEDURE :: INC_GPU_TO_CPU_TRANSFER
END TYPE GPU_STATS

TYPE, ABSTRACT :: FIELD_BASIC
  LOGICAL :: THREAD_BUFFER = .FALSE.

  INTEGER(KIND=JPIM) :: ISTATUS = 0

  TYPE(GPU_STATS) :: STATS

  LOGICAL :: LOBJECT_COPIED = .FALSE.
  LOGICAL :: MAP_DEVPTR = .TRUE.
  INTEGER(KIND=JPIM) :: BLKID = 0

CONTAINS
  PROCEDURE (FIELD_BASIC_SYNC),   DEFERRED :: SYNC_HOST_RDWR
  PROCEDURE (FIELD_BASIC_SYNC),   DEFERRED :: SYNC_HOST_RDONLY
  PROCEDURE (FIELD_BASIC_SYNC),   DEFERRED :: SYNC_DEVICE_RDWR
  PROCEDURE (FIELD_BASIC_SYNC),   DEFERRED :: SYNC_DEVICE_RDONLY
  PROCEDURE (FIELD_BASIC_SYNC_FORCE), DEFERRED :: SYNC_HOST_FORCE
  PROCEDURE (FIELD_BASIC_SYNC_FORCE), DEFERRED :: SYNC_DEVICE_FORCE
  PROCEDURE (FIELD_BASIC_DELETE_DEVICE_DATA), DEFERRED :: DELETE_DEVICE_DATA
  PROCEDURE (FIELD_BASIC_CREATE_DEVICE_DATA), DEFERRED :: CREATE_DEVICE_DATA
#ifdef WITH_FIAT
  PROCEDURE (FIELD_BASIC_CRC64),  DEFERRED :: CRC64
#endif
  PROCEDURE :: SET_CHILDREN_DEVPTR => FIELD_BASIC_SET_CHILDREN_DEVPTR
  PROCEDURE :: SET_DEVICE_DIRTY => FIELD_BASIC_SET_DEVICE_DIRTY
  PROCEDURE :: SET_UNALLOCATED => FIELD_BASIC_SET_UNALLOCATED
  PROCEDURE :: FORCE_DEVICE_FRESH => FIELD_BASIC_FORCE_DEVICE_FRESH
  PROCEDURE :: FORCE_HOST_FRESH => FIELD_BASIC_FORCE_HOST_FRESH
  PROCEDURE :: FORCE_UNDEFINED => FIELD_BASIC_FORCE_UNDEFINED
  PROCEDURE :: SET_STATUS => FIELD_BASIC_SET_STATUS
  PROCEDURE :: GET_STATUS => FIELD_BASIC_GET_STATUS
  PROCEDURE :: IS_UNALLOCATED => FIELD_BASIC_IS_UNALLOCATED
  PROCEDURE :: IS_UNINITIALIZED => FIELD_BASIC_IS_UNINITIALIZED
  PROCEDURE :: IS_UNDEFINED => FIELD_BASIC_IS_UNDEFINED
END TYPE FIELD_BASIC

PUBLIC :: FIELD_BASIC

ABSTRACT INTERFACE
  SUBROUTINE FIELD_BASIC_SYNC (SELF)
    IMPORT FIELD_BASIC
    CLASS(FIELD_BASIC) :: SELF
  END SUBROUTINE
  SUBROUTINE FIELD_BASIC_SYNC_FORCE (SELF, QUEUE, BLK_BOUNDS, OFFSET)
    IMPORT FIELD_BASIC
    CLASS(FIELD_BASIC) :: SELF
    INTEGER, OPTIONAL,  INTENT(IN)    :: QUEUE
    INTEGER, OPTIONAL,  INTENT(IN)    :: BLK_BOUNDS(2)
    INTEGER, OPTIONAL,  INTENT(IN)    :: OFFSET

  END SUBROUTINE
  SUBROUTINE FIELD_BASIC_DELETE_DEVICE_DATA (SELF)
    IMPORT FIELD_BASIC
    CLASS(FIELD_BASIC) :: SELF
  END SUBROUTINE
  SUBROUTINE FIELD_BASIC_CREATE_DEVICE_DATA (SELF, BLK_BOUNDS)
    IMPORT FIELD_BASIC
    CLASS(FIELD_BASIC) :: SELF
    INTEGER, OPTIONAL,  INTENT(IN) :: BLK_BOUNDS(2)
  END SUBROUTINE
#ifdef WITH_FIAT
  INTEGER*8 FUNCTION FIELD_BASIC_CRC64 (SELF)
    IMPORT FIELD_BASIC
    CLASS(FIELD_BASIC) :: SELF
  END FUNCTION
#endif
END INTERFACE

PUBLIC :: FIELD_BASIC_SYNC

CONTAINS

SUBROUTINE INC_CPU_TO_GPU_TRANSFER(SELF, START, FINISH)
  CLASS(GPU_STATS), INTENT(INOUT) :: SELF
  REAL, INTENT(IN) :: START, FINISH
  SELF%TRANSFER_CPU_TO_GPU = SELF%TRANSFER_CPU_TO_GPU + 1
  SELF%TOTAL_TIME_TRANSFER_CPU_TO_GPU = SELF%TOTAL_TIME_TRANSFER_CPU_TO_GPU + FINISH - START
END SUBROUTINE

SUBROUTINE INC_GPU_TO_CPU_TRANSFER(SELF, START, FINISH)
  CLASS(GPU_STATS), INTENT(INOUT) :: SELF
  REAL, INTENT(IN) :: START, FINISH
  SELF%TRANSFER_GPU_TO_CPU = SELF%TRANSFER_GPU_TO_CPU + 1
  SELF%TOTAL_TIME_TRANSFER_GPU_TO_CPU = SELF%TOTAL_TIME_TRANSFER_GPU_TO_CPU + FINISH - START
END SUBROUTINE

SUBROUTINE FIELD_BASIC_SET_CHILDREN_DEVPTR (SELF)

USE FIELD_ABORT_MODULE

CLASS (FIELD_BASIC) :: SELF

CALL FIELD_ABORT ('FIELD_BASIC_SET_CHILDREN_DEVPTR: SHOULD NOT BE CALLED')

END SUBROUTINE

SUBROUTINE FIELD_BASIC_SET_DEVICE_DIRTY (SELF)

CLASS (FIELD_BASIC) :: SELF

IF (IAND (SELF%GET_STATUS (), NDEVFRESH)==NDEVFRESH) THEN
  CALL SELF%SET_STATUS (IAND (SELF%GET_STATUS (), NOT (NDEVFRESH)))
ENDIF

END SUBROUTINE FIELD_BASIC_SET_DEVICE_DIRTY

SUBROUTINE FIELD_BASIC_SET_UNALLOCATED (SELF)

CLASS (FIELD_BASIC) :: SELF

CALL SELF%SET_STATUS( IOR( IAND(SELF%GET_STATUS(), NOT(UNALLOCATED)) , UNALLOCATED))

END SUBROUTINE FIELD_BASIC_SET_UNALLOCATED

SUBROUTINE FIELD_BASIC_FORCE_DEVICE_FRESH (SELF)

CLASS (FIELD_BASIC) :: SELF

CALL SELF%SET_STATUS(NDEVFRESH)

END SUBROUTINE FIELD_BASIC_FORCE_DEVICE_FRESH

SUBROUTINE FIELD_BASIC_FORCE_HOST_FRESH (SELF)

CLASS (FIELD_BASIC) :: SELF

CALL SELF%SET_STATUS(NHSTFRESH)

END SUBROUTINE FIELD_BASIC_FORCE_HOST_FRESH

SUBROUTINE FIELD_BASIC_FORCE_UNDEFINED (SELF)

CLASS (FIELD_BASIC) :: SELF
INTEGER(KIND=JPIM)  :: NOT_FRESH

NOT_FRESH = NOT(IOR(NHSTFRESH, NDEVFRESH))
CALL SELF%SET_STATUS( IOR( IAND(SELF%GET_STATUS(), NOT_FRESH) , UNDEFINED))

END SUBROUTINE FIELD_BASIC_FORCE_UNDEFINED

SUBROUTINE FIELD_BASIC_SET_STATUS (SELF, KSTATUS)

CLASS (FIELD_BASIC) :: SELF
INTEGER (KIND=JPIM), INTENT (IN) :: KSTATUS

SELF%ISTATUS = KSTATUS

END SUBROUTINE FIELD_BASIC_SET_STATUS

INTEGER (KIND=JPIM) FUNCTION FIELD_BASIC_GET_STATUS (SELF)

CLASS (FIELD_BASIC) :: SELF

FIELD_BASIC_GET_STATUS = SELF%ISTATUS

END FUNCTION

FUNCTION FIELD_BASIC_IS_UNALLOCATED (SELF) RESULT(IS_UNALLOCATED)

CLASS (FIELD_BASIC) :: SELF
LOGICAL :: IS_UNALLOCATED

IS_UNALLOCATED = ( UNALLOCATED == IAND(SELF%GET_STATUS(), UNALLOCATED) )

END FUNCTION FIELD_BASIC_IS_UNALLOCATED

FUNCTION FIELD_BASIC_IS_UNINITIALIZED (SELF) RESULT(IS_UNINITIALIZED)

CLASS (FIELD_BASIC) :: SELF
LOGICAL :: IS_UNINITIALIZED

IS_UNINITIALIZED = ( UNINITIALIZED == IAND(SELF%GET_STATUS(), UNINITIALIZED) )

END FUNCTION FIELD_BASIC_IS_UNINITIALIZED

FUNCTION FIELD_BASIC_IS_UNDEFINED (SELF) RESULT(IS_UNDEFINED)

CLASS (FIELD_BASIC) :: SELF
LOGICAL :: IS_UNDEFINED

IS_UNDEFINED = ( UNDEFINED == IAND(SELF%GET_STATUS(), UNDEFINED) )

END FUNCTION FIELD_BASIC_IS_UNDEFINED

END MODULE FIELD_BASIC_MODULE
