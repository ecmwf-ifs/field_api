! (C) Copyright 2022- ECMWF.
! (C) Copyright 2022- Meteo-France.
! (C) Copyright 2023- NVIDIA
!
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.

MODULE HDF5_COMMON_MODULE

USE HDF5, ONLY: H5open_F, H5Fopen_F, H5Fcreate_F, H5Dclose_F, H5Sclose_F, H5Fclose_F, H5close_F, H5pclose_f
USE HDF5, ONLY: H5PCREATE_F, H5PSET_FAPL_MPIO_F, H5FD_MPIO_COLLECTIVE_F, H5P_DATASET_XFER_F, H5PSET_DXPL_MPIO_F
USE HDF5, ONLY: HID_T, H5F_ACC_RDWR_F,  H5F_ACC_TRUNC_F
USE HDF5, ONLY: H5P_FILE_ACCESS_F
USE MPL_MODULE, ONLY: MPL_COMM
USE MPL_MODULE, ONLY: MPL_RANK, MPL_NUMPROC

IMPLICIT NONE

CONTAINS

SUBROUTINE INIT_HDF5_STORAGE()

  INTEGER :: HDFERR

  CALL H5open_F(HDFERR)

END SUBROUTINE INIT_HDF5_STORAGE

SUBROUTINE OPEN_HDF5_STORAGE(FILENAME, FILE_ID, PLIST_ID )

  CHARACTER(LEN=*), INTENT(IN)  :: FILENAME
  INTEGER(HID_T)  , INTENT(OUT) :: FILE_ID
  INTEGER(HID_T)  , OPTIONAL    :: PLIST_ID
  INTEGER :: HDFERR
  LOGICAL :: HDF5_FILE_EXISTS, HDF5_DATASET_EXISTS 

  INQUIRE(FILE=TRIM(FILENAME), EXIST=HDF5_FILE_EXISTS)
  IF (PRESENT(PLIST_ID)) THEN
    IF (HDF5_FILE_EXISTS) THEN
        CALL H5Fopen_F(  TRIM(FILENAME), H5F_ACC_RDWR_F , FILE_ID, HDFERR, ACCESS_PRP = PLIST_ID)
    ELSE
        CALL H5Fcreate_F(TRIM(FILENAME), H5F_ACC_TRUNC_F, FILE_ID, HDFERR, ACCESS_PRP = PLIST_ID)
    END IF
  ELSE
    IF (HDF5_FILE_EXISTS) THEN
        CALL H5Fopen_F(  TRIM(FILENAME), H5F_ACC_RDWR_F , FILE_ID, HDFERR)
    ELSE
        CALL H5Fcreate_F(TRIM(FILENAME), H5F_ACC_TRUNC_F, FILE_ID, HDFERR)
    END IF
  END IF

END SUBROUTINE OPEN_HDF5_STORAGE

SUBROUTINE CREATE_HDF5_FILE_PLIST(PLIST_ID)

  INTEGER(HID_T) :: PLIST_ID 
  INTEGER :: HDFERR

  CALL H5PCREATE_F(H5P_FILE_ACCESS_F, PLIST_ID, HDFERR)
  CALL H5PSET_FAPL_MPIO_F(PLIST_ID ,MPL_COMM, 0, HDFERR)
 

END SUBROUTINE CREATE_HDF5_FILE_PLIST

SUBROUTINE CREATE_HDF5_DSET_PLIST(PLIST_ID)

  INTEGER(HID_T) :: PLIST_ID 
  INTEGER :: HDFERR

  CALL H5PCREATE_F(H5P_DATASET_XFER_F, PLIST_ID, HDFERR)
  CALL H5PSET_DXPL_MPIO_F(PLIST_ID, H5FD_MPIO_COLLECTIVE_F, HDFERR)
 

END SUBROUTINE CREATE_HDF5_DSET_PLIST

SUBROUTINE CLOSE_HDF5_STORAGE(DSET_ID, SPACE_ID, FILE_ID, PLIST_ID)

  INTEGER(HID_T) :: FILE_ID, SPACE_ID
  INTEGER(HID_T) :: DSET_ID
  INTEGER(HID_T), OPTIONAL :: PLIST_ID
  INTEGER :: HDFERR

  IF(PRESENT(PLIST_ID)) CALL H5Pclose_F(PLIST_ID, HDFERR)
  CALL H5Dclose_F( DSET_ID, HDFERR)
  CALL H5Sclose_F(SPACE_ID, HDFERR)
  CALL H5Fclose_F( FILE_ID, HDFERR)

END SUBROUTINE CLOSE_HDF5_STORAGE

SUBROUTINE FINALIZE_HDF5_STORAGE()

  INTEGER :: HDFERR

  CALL H5close_F( HDFERR)

END SUBROUTINE FINALIZE_HDF5_STORAGE

SUBROUTINE NAME_HDF5_STORAGE(FILENAME,FNAME_WITH_RANK)

CHARACTER(LEN=*),              INTENT(IN)  :: FILENAME
CHARACTER(LEN=:), ALLOCATABLE, INTENT(OUT) :: FNAME_WITH_RANK
CHARACTER(LEN=:), ALLOCATABLE :: RANK_STR
INTEGER :: NAME_LEN, RANK_LEN
RANK_LEN = CEILING(LOG10(REAL(ABS(MPL_RANK)+1)))  
ALLOCATE(CHARACTER(RANK_LEN) :: RANK_STR)
IF(MPL_NUMPROC > 1) THEN
   WRITE(RANK_STR, '(I0)') MPL_RANK
   NAME_LEN = LEN_TRIM(FILENAME) + LEN("_rank") + LEN_TRIM(RANK_STR) + LEN(".hdf5")
   ALLOCATE(CHARACTER(LEN=NAME_LEN) :: FNAME_WITH_RANK)
   FNAME_WITH_RANK = TRIM(FILENAME) // "_rank" // TRIM(RANK_STR) // ".hdf5"
ELSE
   NAME_LEN = LEN_TRIM(FILENAME) + LEN(".hdf5")
   ALLOCATE(CHARACTER(LEN=NAME_LEN) :: FNAME_WITH_RANK)
   FNAME_WITH_RANK = TRIM(FILENAME) // ".hdf5"
ENDIF

END SUBROUTINE NAME_HDF5_STORAGE

END MODULE HDF5_COMMON_MODULE
